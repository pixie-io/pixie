package(default_visibility = ["//visibility:public"])

load(":images.bzl", "generate_vizier_bootstrap_yamls", "generate_vizier_yamls", "image_map_with_bundle_version")
load("@io_bazel_rules_docker//container:container.bzl", "container_bundle", "container_image", "container_pull")
load("@io_bazel_rules_docker//contrib:push-all.bzl", "container_push")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")

VIZIER_IMAGE_MAP = {
    "gcr.io/pl-dev-infra/vizier/pem_image": "//src/vizier/services/agent/pem:pem_image",
    "gcr.io/pl-dev-infra/vizier/kelvin_image": "//src/vizier/services/agent/kelvin:kelvin_image",
    "gcr.io/pl-dev-infra/vizier/cloud_connector_server_image": "//src/vizier/services/cloud_connector:cloud_connector_server_image",
    "gcr.io/pl-dev-infra/vizier/metadata_server_image": "//src/vizier/services/metadata:metadata_server_image",
    "gcr.io/pl-dev-infra/vizier/query_broker_server_image": "//src/vizier/services/query_broker:query_broker_server_image",
    "gcr.io/pl-dev-infra/vizier/certmgr_server_image": "//src/vizier/services/certmgr:certmgr_server_image",
    "gcr.io/pl-dev-infra/vizier/fluent-bit-image": "//src/vizier/utils/fluent_bit_nats_plugin:fluentbit_nats_plugin_image",
    "gcr.io/pl-dev-infra/vizier/vizier_updater_image": "//src/utils/pixie_updater:vizier_updater_image",
    "gcr.io/pl-dev-infra/vizier/cert_provisioner_image": "//src/utils/cert_provisioner:cert_provisioner_image",
}

generate_vizier_yamls(
    name = "private_vizier_prod",
    srcs = glob(["**/*.yaml"]),
    out = "private_vizier_prod.yaml",
    image_map = VIZIER_IMAGE_MAP,
    public = False,
)

generate_vizier_yamls(
    name = "public_vizier_prod",
    srcs = glob(["**/*.yaml"]),
    out = "public_vizier_prod.yaml",
    image_map = VIZIER_IMAGE_MAP,
    public = True,
)

generate_vizier_bootstrap_yamls(
    name = "public_vizier_bootstrap_prod",
    srcs = glob(["**/*.yaml"]),
    out = "public_vizier_bootstrap_prod.yaml",
    image_map = VIZIER_IMAGE_MAP,
    public = True,
)

generate_vizier_bootstrap_yamls(
    name = "private_vizier_bootstrap_prod",
    srcs = glob(["**/*.yaml"]),
    out = "private_vizier_bootstrap_prod.yaml",
    image_map = VIZIER_IMAGE_MAP,
    public = False,
)

container_bundle(
    name = "private_vizier_images_bundle",
    images = image_map_with_bundle_version(VIZIER_IMAGE_MAP, False),
)

container_bundle(
    name = "public_vizier_images_bundle",
    images = image_map_with_bundle_version(VIZIER_IMAGE_MAP, True),
)

container_push(
    name = "vizier_images_push",
    bundle = select({
        ":public": ":public_vizier_images_bundle",
        "//conditions:default": ":private_vizier_images_bundle",
    }),
    format = "Docker",
)

config_setting(
    name = "public",
    values = {"define": "public=True"},
)

pkg_tar(
    name = "vizier_yamls",
    remap_paths = {
        "/vizier/private_vizier_bootstrap_prod.yaml": "vizier/vizier_bootstrap_prod.yaml",
        "/vizier/public_vizier_bootstrap_prod.yaml": "vizier/vizier_bootstrap_prod.yaml",
        "/vizier/private_vizier_prod.yaml": "vizier/vizier_prod.yaml",
        "/vizier/public_vizier_prod.yaml": "vizier/vizier_prod.yaml",
    },
    srcs =
        select({
            "//conditions:default": ["//k8s/vizier:private_vizier_bootstrap_prod.yaml"],
            ":public": ["//k8s/vizier:public_vizier_bootstrap_prod.yaml"],
        }) +
        select({
            "//conditions:default": ["//k8s/vizier:private_vizier_prod.yaml"],
            ":public": ["//k8s/vizier:public_vizier_prod.yaml"],
        }) +
        [
            "//k8s/vizier_deps:etcd_prod",
            "//k8s/vizier_deps:nats_prod",
            "//k8s/vizier_deps:etcd_operator_prod",
        ],
    package_dir = "/yamls",
    strip_prefix = "/k8s",
)
