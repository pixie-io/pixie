# Copyright 2018- The Pixie Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("//bazel:pl_qemu_kernels.bzl", "kernel_flag_name", "qemu_image_to_deps")
load("//bazel:toolchain_transitions.bzl", "qemu_interactive_runner")
load("//bazel/test_runners/qemu_with_kernel:runner.bzl", "qemu_with_kernel_test_runner")

exports_files(["interactive_runner.sh"])

kernel_image_deps = qemu_image_to_deps()

string_flag(
    name = "kernel_version",
    build_setting_default = "latest",
    values = [v for v in kernel_image_deps.keys()],
    visibility = ["//visibility:public"],
)

[config_setting(
    name = kernel_flag_name(version),
    flag_values = {
        ":kernel_version": version,
    },
) for version in kernel_image_deps.keys()]

kernel_select_list = {kernel_flag_name(version): dep for version, dep in kernel_image_deps.items()}

qemu_with_kernel_test_runner(
    name = "runner",
    kernel_image = select(kernel_select_list),
    visibility = ["//visibility:public"],
)

qemu_interactive_runner(
    name = "run_interactive",
    tags = [
        "manual",
        "qemu_interactive",
    ],
    visibility = ["//visibility:public"],
)
