# Copyright 2018- The Pixie Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_docker//java:image.bzl", "DEFAULT_JAVA_BASE")
load("@io_bazel_rules_docker//scala:image.bzl", "scala_image")
load("@io_bazel_rules_scala//scala:scala.bzl", "scala_binary", "scala_library")
load("@io_bazel_rules_scala//thrift:thrift.bzl", "thrift_library")
load("@io_bazel_rules_scala//twitter_scrooge:twitter_scrooge.bzl", "scrooge_scala_library")
load("@package_bundle//file:packages.bzl", "packages")

package(default_visibility = ["//src/stirling:__subpackages__"])

genrule(
    name = "certs",
    outs = [
        "ca.crt",
        "client.crt",
        "client.key",
        "server.crt",
        "server.key",
    ],
    cmd = """
        $(location //src/common/testing/test_utils/cert_generator:cert_generator) \
        --ca_crt $(location ca.crt) \
        --client_crt $(location client.crt) \
        --client_key $(location client.key) \
        --server_crt $(location server.crt) \
        --server_key $(location server.key) \
        --secret_key_type pkcs8
    """,
    tools = [
        "//src/common/testing/test_utils/cert_generator",
    ],
)

pkg_tar(
    name = "ssl_keys",
    srcs = [
        "ca.crt",
        "client.crt",
        "client.key",
        "server.crt",
        "server.key",
    ],
    mode = "0755",
    package_dir = "/etc/ssl",
    strip_prefix = "/src/stirling/source_connectors/socket_tracer/testing/containers/thriftmux",
)

# TODO(ddelnano): byteman is required until dealing with
# netty's libnetty_tcnative.so's random file name is solved for.
# See #407 for more details.
pkg_tar(
    name = "byteman_rule",
    srcs = [
        "byteman_rule.txt",
    ],
    mode = "0755",
    package_dir = "/etc/byteman",
    strip_prefix = "/src/stirling/source_connectors/socket_tracer/testing/containers/thriftmux",
)

thrift_library(
    name = "thrift_library",
    srcs = glob(["**/*.thrift"]),
)

scala_library(
    name = "scrooge_jars",
    visibility = ["//visibility:public"],
    exports = [
        "@maven//:com_twitter_finagle_core_2_13",
        "@maven//:com_twitter_finagle_http_2_13",
        "@maven//:com_twitter_finagle_mux_2_13",
        "@maven//:com_twitter_finagle_thrift_2_13",
        "@maven//:com_twitter_finagle_thriftmux_2_13",
        "@maven//:com_twitter_scrooge_core_2_13",
    ],
)

scala_library(
    name = "thrift_jars",
    visibility = ["//visibility:public"],
    exports = [
        "@maven//:org_apache_thrift_libthrift",
    ],
)

scrooge_scala_library(
    name = "thriftmux_scrooge",
    deps = [
        ":thrift_library",
    ],
)

scala_library(
    name = "logging",
    deps = [
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

scala_binary(
    name = "server_bin",
    srcs = glob(["**/*.scala"]),
    main_class = "Server",
    deps = [
        ":thriftmux_scrooge",
    ],
)

scala_binary(
    name = "client_bin",
    srcs = glob(["**/*.scala"]),
    main_class = "Client",
    deps = [
        ":thriftmux_scrooge",
    ],
)

container_image(
    name = "thriftmux_base",
    base = DEFAULT_JAVA_BASE,
    debs = [
        packages["libbyteman-java"],
        packages["libcrypt1"],
    ],
    tars = [
        ":ssl_keys",
        ":byteman_rule",
    ],
)

scala_image(
    name = "server_image",
    srcs = glob(["**/*.scala"]),
    base = ":thriftmux_base",
    jvm_flags = [
        "-javaagent:/usr/share/java/byteman-4.0.11.jar=script:/etc/byteman/byteman_rule.txt,boot:/usr/share/java/byteman-4.0.11.jar",
        "-Dorg.jboss.byteman.transform.all",
        "-Dio.netty.native.deleteLibAfterLoading=false",
    ],
    main_class = "Server",
    deps = [
        ":logging",
        ":thriftmux_scrooge",
    ],
)
